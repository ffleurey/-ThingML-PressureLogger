import "SharedMessages.thingml"

thing ArduinoSensor includes LoggerMsgs, TimerMsgs 
@c_header "#include <ifx_dps310.h>"
@c_global "
IFX_Dps310 ifxDps310_1 = IFX_Dps310();
IFX_Dps310 ifxDps310_2 = IFX_Dps310();
"
{
	
	provided port gateway {
		sends pressure
	}
	
	required port clock {
		receives sample_clock
	}

	function initialize_sensors() do
		'ifxDps310_1.begin(Wire, 0x76);
  		 ifxDps310_2.begin(Wire, 0x77);
  		 ifxDps310_1.correctTemp();
  		 ifxDps310_2.correctTemp();'
	end
	
	
	function sample_pressure() do
		 var p1 : Long
		 var p2 : Long
		 'ifxDps310_1.measurePressureOnce('&p1&', 1);'
		 'ifxDps310_2.measurePressureOnce('&p2&', 1);'
		 gateway!pressure(p1, p2)
	end
	
	statechart Main init SAMPLING {
		
		on entry initialize_sensors()
		
		state SAMPLING {

			internal event clock?sample_clock action sample_pressure() 
		}	
	}
}

protocol Serial @serializer "Binary";

configuration ArduinoSensor {
	instance sensor : ArduinoSensor
	
	connector sensor.clock over Timer
	connector sensor.gateway over Serial
}